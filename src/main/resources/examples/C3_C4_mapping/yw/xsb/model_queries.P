% MQ1:  Where is the definition of program block fetch_monthly_mean_precipitation_data?
:- table mq1/3.
mq1(SourceFile, StartLine, EndLine) :-
    program_source('C3_C4_map_present_NA.fetch_monthly_mean_precipitation_data', SourceFile, StartLine, EndLine).
    
% MQ2:  What is the name of the top-level workflow?
:- table mq2/1.
mq2(WorkflowName) :-
    top_workflow(W),
    program(W, _, WorkflowName, _, _).

% MQ3:  What are the names of the program blocks comprising the workflow?
:- table mq3/1.
mq3(ProgramName) :-
    top_workflow(W),
    has_subprogram(W, P),
    program(P, ProgramName, _, _, _).

% MQ4:  What are the names of the program blocks in the workflow that produce workflow outputs?
:- table mq4/1.
mq4(ProgramName) :-
    top_workflow(W),
    has_out_port(W, WorkflowOutputPort),
    outflow_connects_to_channel(WorkflowOutputPort, WorkflowOutputChannel),
    port_connects_to_channel(ProgramOutputPort, WorkflowOutputChannel),
    has_out_port(Program, ProgramOutputPort),
    program(Program, ProgramName, _, _, _).

% MQ5:  What are the inputs to the script?
:- table mq5/1.
mq5(InputPortName) :-
    top_workflow(W),
    has_in_port(W, P),
    port(P, _, InputPortName, _, _, _).

% MQ6:  What data is output by program block examine_pixels_for_grass?
:- table mq6/1.
mq6(DataName) :-
    program(P, _, 'C3_C4_map_present_NA.examine_pixels_for_grass', _, _),
    has_out_port(P, OUT),
    port_data(OUT, DataName, _).
    
% MQ7: What program blocks provide input directly to generate_netcdf_file_for_Grass_fraction?
:- table mq7/1.
mq7(ProgramName) :-
    program(P1, _, 'C3_C4_map_present_NA.generate_netcdf_file_for_Grass_fraction', _, _),
    has_in_port(P1, IN),
    port_data(IN, _, D),
    port_data(OUT, _, D),
    has_out_port(P2, OUT),
    program(P2, ProgramName, _, _, _).

% MQ8: What programs have input ports that receive data lon_variable?
:- table mq8/1.
mq8(ProgramName) :-
    data(D, _, 'C3_C4_map_present_NA[lon_variable]'),
    channel(C, D),
    port_connects_to_channel(IN, C),
    has_in_port(P, IN),
    program(P, ProgramName, _, _, _).

% MQ9: How many ports read data lat_variable?
:- table mq9/1.
mq9(PortCount) :-
    data(D, _, 'C3_C4_map_present_NA[lat_variable]'),
    count(data_in_port(_, D), PortCount).

% MQ10: How many data are read by more than port in workflow C3_C4_map_present_NA?
:- table mq10/1.
mq10(DataCount) :- 
    program(W, 'C3_C4_map_present_NA', _, _, _),
    count(data_in_workflow_read_by_multiple_ports(_, W), DataCount).
:- table data_in_workflow_read_by_multiple_ports/2.
data_in_workflow_read_by_multiple_ports(D, W) :-
    has_subprogram(W, P),
    has_in_port(P, IN),
    port_connects_to_channel(IN, C),
    channel(C, D),
    data_in_port_count(Count, D),
    Count > 1.
:- table data_in_port_count/2.
data_in_port_count(PortCount, D) :-
    data(D, _, _),
    count(data_in_port(_, D), PortCount).
    
% MQ11: What program blocks are immediately downstream of examine_pixels_for_grass?
:- table mq11/1.
mq11(DownstreamProgramName) :-
    program(P1, DownstreamProgramName, _, _, _),
    program(P2, _, 'C3_C4_map_present_NA.examine_pixels_for_grass', _, _),
    program_immediately_downstream(P1, P2).

% MQ12: What program blocks are immediately upstream of generate_netcdf_file_for_Grass_fraction?
:- table mq12/1.
mq12(UpstreamProgramName) :-
    program(P1, UpstreamProgramName, _, _, _),
    program(P2, _, 'C3_C4_map_present_NA.generate_netcdf_file_for_Grass_fraction', _, _),
    program_immediately_upstream(P1, P2).
    
% MQ13: What program blocks are upstream of generate_netcdf_file_for_C3_fraction?
:- table mq13/1.
mq13(UpstreamProgramName):-
    program(P1, UpstreamProgramName, _, _, _),
    program(P2, _, 'C3_C4_map_present_NA.generate_netcdf_file_for_C3_fraction', _, _),
    program_upstream(P1, P2).

% MQ14: What program blocks are anywhere downstream of fetch_monthly_mean_precipitation_data?
:- table mq14/1.
mq14(DownstreamProgramName):-
    program(P1, DownstreamProgramName, _, _, _),
    program(P2, _, 'C3_C4_map_present_NA.fetch_monthly_mean_precipitation_data', _, _),
    program_downstream(P1, P2).

% MQ15: What data is immediately downstream of Tair_Matrix?
:- table mq15/1.
mq15(DownstreamDataName) :-
    data(D1, DownstreamDataName, _),
    data(D2, 'Tair_Matrix', _),
    data_immediately_downstream(D1, D2).

% MQ16: What data is immediately upstream of Grass_fraction_data?
:- table mq16/1.
mq16(UpstreamDataName) :-
    data(D1, UpstreamDataName, _),
    data(D2, 'Grass_fraction_data', _),
    data_immediately_upstream(D1, D2).

% MQ17: What data is downstream of Rain_Matrix?
:- table mq17/1.
mq17(DownstreamDataName):-
    data(D1, DownstreamDataName, _),
    data(D2, 'Rain_Matrix', _),
    data_downstream(D1, D2).

% MQ18: What data is upstream of SYNMAP_land_cover_map_variable?
:- table mq18/1.
mq18(UpstreamDataName):-
    data(D1, UpstreamDataName, _),
    data(D2, 'SYNMAP_land_cover_map_variable', _),
    data_upstream(D1, D2).

% MQ19: What URI variables are associated with reads of data mean_airtemp?
:- table mq19/1.
mq19(VariableName) :-
    data(D, _, 'C3_C4_map_present_NA[mean_airtemp]'),
    channel(C, D),
    port_connects_to_channel(IN, C),
    has_in_port(_, IN),
    uri_variable(_, VariableName, IN).

% MQ20: What URI variables do data read into mean_precip and mean_airtemp have in common?
:- table mq20/1.
mq20(VariableName) :-
    data(D1, _, 'C3_C4_map_present_NA[mean_precip]'),
    data(D2, _, 'C3_C4_map_present_NA[mean_airtemp]'),
    input_data_has_uri_variable(D1, V1),
    input_data_has_uri_variable(D2, V2),
    uri_variable(V1, VariableName, _),
    uri_variable(V2, VariableName, _).

